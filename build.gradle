plugins {
    id 'fabric-loom' version '0.11-SNAPSHOT'
    id 'org.jetbrains.kotlin.jvm' version '1.6.20'
}

sourceCompatibility = targetCompatibility = project.java_version

archivesBaseName = project.mod_name
version project.mod_version
group project.mod_group

loom {
    accessWidenerPath = file('src/main/resources/transrights.accesswidener')
}

tasks.withType(JavaCompile).configureEach {
    it.options.release.set(17)
}

compileKotlin.kotlinOptions {
    jvmTarget = project.java_version as String
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'name', project.name

    filesMatching('fabric.mod.json') {
        expand 'version': project.mod_version, 'id': project.mod_name
    }
}

dependencies {
    Set<String> libraries = [
            'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1',
            'org.jetbrains.kotlin:kotlin-reflect:1.6.20'
    ]

    // fabric dependencies
    minecraft "com.mojang:minecraft:$project.minecraft_version"
    mappings "net.fabricmc:yarn:$project.minecraft_version+$project.yarn_mappings:v2"
    modImplementation 'net.fabricmc:fabric-loader:0.13.3'

    // TR dependencies
    libraries.forEach {library ->
        include modImplementation(library)
    }
}

jar {
    from('LICENSE') {
        rename {
            "${it}_$project.archivesBaseName"
        }
    }

    manifest.attributes('Main-Class': 'trans.rights.Main')
}
