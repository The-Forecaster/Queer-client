plugins {
    id 'fabric-loom' version '0.10-SNAPSHOT'
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
}

sourceCompatibility = targetCompatibility = project.java_version as int

archivesBaseName = project.mod_name
version = project.mod_version
group = project.mod_group

loom {
    accessWidenerPath = file("src/main/resources/transrights.accesswidener")
}

repositories {
    jcenter()

    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }
}

dependencies {
    Set<String> apiModules = [
    ]

    Set<String> libraries = [
        "com.github.ZeroMemes:Alpine:1.9",
        "net.jodah:typetools:0.6.3"
    ]

    // fabric dependencies
    minecraft "com.mojang:minecraft:$project.minecraft_version"
    mappings "net.fabricmc:yarn:$project.minecraft_version+$project.yarn_mappings:v2"
    modImplementation "net.fabricmc:fabric-loader:0.12.12"

    // api dependencies
    apiModules.forEach {
        include(modImplementation(fabricApi.module(it, "0.46.2+1.18")))
    }

    // TR dependencies
    libraries.forEach {
        include(modImplementation(it))
    }
}

jar {
    from("LICENSE") {
        rename {
            "$it-$project.mod_name" 
        }
    }

    manifest {
	    attributes("Main-Class" : "me.austin.queer.Main")
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

tasks.withType(JavaCompile).configureEach {
    it.options.release.set(17)

    it.options.encoding = "UTF-8"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = project.java_version as String
    }
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.mod_version
    }
}
