plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'org.jetbrains.kotlin.jvm' version '1.6.10'
	id 'com.github.johnrengelman.shadow' version '7.1.1'
}

sourceCompatibility = targetCompatibility = project.java_version as int
archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
 	accessWidenerPath = file("src/main/resources/transrights.accesswidener")
}

repositories {
	jcenter()

	maven {
        name = 'jitpack.io'
        url =  'https://jitpack.io'
    }
	
	maven {
		name = 'impactdevelopment-repo'
		url = 'https://impactdevelopment.github.io/maven/'
	}
}

dependencies {
	Set<String> apiModules = [
		"fabric-api-base",
		"fabric-resource-loader-v0"
	]

	Set<String> libraries = [
		"com.github.ZeroMemes:Alpine:${project.alpine_version}",
		"club.minnced:java-discord-rpc:${project.discord_rpc_version}",
		"org.reflections:reflections:${reflections_version}"
	]

	// fabric dependencies
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	include(implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:${project.kotlin_version}"))

	// api dependencies
	apiModules.forEach {
		include(modImplementation(fabricApi.module(it, "${project.fabric_version}")))
	}

	// TR dependencies
	libraries.forEach {
		shadow(implementation(it))
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from("LICENSE") {
		rename { 
			"${ it }-${ project.archivesBaseName }" 
		}
	}

	manifest {
		attributes("Main-Class" : "me.austin.queer.Main")
	}
}

shadowJar {
	configurations = [ project.configurations.shadow ]
}

remapJar {
	dependsOn shadowJar
	input.set(shadowJar.archiveFile)
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(project.java_version as int)
}

compileKotlin {
	kotlinOptions {
        jvmTarget = project.java_version as String
    }
}

processResources {
	inputs.property "version", project.mod_version

	filesMatching("fabric.mod.json") {
		expand "version": project.mod_version
	}
}